EMERGENCY Released VIAB*1*10 SEQ #9
Extracted from mail message
**KIDS**:VIAB*1.0*10^

**INSTALL NAME**
VIAB*1.0*10
"BLD",10018,0)
VIAB*1.0*10^VISTA INTEGRATION ADAPTER^0^3170317^y
"BLD",10018,4,0)
^9.64PA^^
"BLD",10018,6.3)
3
"BLD",10018,"ABPKG")
n
"BLD",10018,"KRN",0)
^9.67PA^779.2^20
"BLD",10018,"KRN",.4,0)
.4
"BLD",10018,"KRN",.401,0)
.401
"BLD",10018,"KRN",.402,0)
.402
"BLD",10018,"KRN",.403,0)
.403
"BLD",10018,"KRN",.5,0)
.5
"BLD",10018,"KRN",.84,0)
.84
"BLD",10018,"KRN",3.6,0)
3.6
"BLD",10018,"KRN",3.8,0)
3.8
"BLD",10018,"KRN",9.2,0)
9.2
"BLD",10018,"KRN",9.8,0)
9.8
"BLD",10018,"KRN",9.8,"NM",0)
^9.68A^1^1
"BLD",10018,"KRN",9.8,"NM",1,0)
VIABMS^^0^B134965720
"BLD",10018,"KRN",9.8,"NM","B","VIABMS",1)

"BLD",10018,"KRN",19,0)
19
"BLD",10018,"KRN",19.1,0)
19.1
"BLD",10018,"KRN",101,0)
101
"BLD",10018,"KRN",409.61,0)
409.61
"BLD",10018,"KRN",771,0)
771
"BLD",10018,"KRN",779.2,0)
779.2
"BLD",10018,"KRN",870,0)
870
"BLD",10018,"KRN",8989.51,0)
8989.51
"BLD",10018,"KRN",8989.52,0)
8989.52
"BLD",10018,"KRN",8994,0)
8994
"BLD",10018,"KRN","B",.4,.4)

"BLD",10018,"KRN","B",.401,.401)

"BLD",10018,"KRN","B",.402,.402)

"BLD",10018,"KRN","B",.403,.403)

"BLD",10018,"KRN","B",.5,.5)

"BLD",10018,"KRN","B",.84,.84)

"BLD",10018,"KRN","B",3.6,3.6)

"BLD",10018,"KRN","B",3.8,3.8)

"BLD",10018,"KRN","B",9.2,9.2)

"BLD",10018,"KRN","B",9.8,9.8)

"BLD",10018,"KRN","B",19,19)

"BLD",10018,"KRN","B",19.1,19.1)

"BLD",10018,"KRN","B",101,101)

"BLD",10018,"KRN","B",409.61,409.61)

"BLD",10018,"KRN","B",771,771)

"BLD",10018,"KRN","B",779.2,779.2)

"BLD",10018,"KRN","B",870,870)

"BLD",10018,"KRN","B",8989.51,8989.51)

"BLD",10018,"KRN","B",8989.52,8989.52)

"BLD",10018,"KRN","B",8994,8994)

"BLD",10018,"QDEF")
^^^^NO^^^^NO^^NO
"BLD",10018,"QUES",0)
^9.62^^
"BLD",10018,"REQB",0)
^9.611^1^1
"BLD",10018,"REQB",1,0)
VIAB*1.0*8^1
"BLD",10018,"REQB","B","VIAB*1.0*8",1)

"MBREQ")
0
"PKG",656,-1)
1^1
"PKG",656,0)
VISTA INTEGRATION ADAPTER^VIAB^VIAB APP
"PKG",656,20,0)
^9.402P^^
"PKG",656,22,0)
^9.49I^1^1
"PKG",656,22,1,0)
1.0
"PKG",656,22,1,"PAH",1,0)
10^3170317
"QUES","XPF1",0)
Y
"QUES","XPF1","??")
^D REP^XPDH
"QUES","XPF1","A")
Shall I write over your |FLAG| File
"QUES","XPF1","B")
YES
"QUES","XPF1","M")
D XPF1^XPDIQ
"QUES","XPF2",0)
Y
"QUES","XPF2","??")
^D DTA^XPDH
"QUES","XPF2","A")
Want my data |FLAG| yours
"QUES","XPF2","B")
YES
"QUES","XPF2","M")
D XPF2^XPDIQ
"QUES","XPI1",0)
YO
"QUES","XPI1","??")
^D INHIBIT^XPDH
"QUES","XPI1","A")
Want KIDS to INHIBIT LOGONs during the install
"QUES","XPI1","B")
NO
"QUES","XPI1","M")
D XPI1^XPDIQ
"QUES","XPM1",0)
PO^VA(200,:EM
"QUES","XPM1","??")
^D MG^XPDH
"QUES","XPM1","A")
Enter the Coordinator for Mail Group '|FLAG|'
"QUES","XPM1","B")

"QUES","XPM1","M")
D XPM1^XPDIQ
"QUES","XPO1",0)
Y
"QUES","XPO1","??")
^D MENU^XPDH
"QUES","XPO1","A")
Want KIDS to Rebuild Menu Trees Upon Completion of Install
"QUES","XPO1","B")
NO
"QUES","XPO1","M")
D XPO1^XPDIQ
"QUES","XPZ1",0)
Y
"QUES","XPZ1","??")
^D OPT^XPDH
"QUES","XPZ1","A")
Want to DISABLE Scheduled Options, Menu Options, and Protocols
"QUES","XPZ1","B")
NO
"QUES","XPZ1","M")
D XPZ1^XPDIQ
"QUES","XPZ2",0)
Y
"QUES","XPZ2","??")
^D RTN^XPDH
"QUES","XPZ2","A")
Want to MOVE routines to other CPUs
"QUES","XPZ2","B")
NO
"QUES","XPZ2","M")
D XPZ2^XPDIQ
"RTN")
1
"RTN","VIABMS")
0^1^B134965720^B138505639
"RTN","VIABMS",1,0)
VIABMS ;AAC/JMC - VIA BMS RPCs ;04/07/2016
"RTN","VIABMS",2,0)
 ;;1.0;VISTA INTEGRATION ADAPTER;**8,10**;06-FEB-2014;Build 3
"RTN","VIABMS",3,0)
 ;
"RTN","VIABMS",4,0)
 ;The routine is in support of the Bed Management System (BMS) and is linked to VIAB BMS  RPC. The RPC 
"RTN","VIABMS",5,0)
 ;determines what data is returned from what is passed in the input parameter VIA("PATH").  All BMS requests
"RTN","VIABMS",6,0)
 ;are handled by this one RPC.
"RTN","VIABMS",7,0)
 ;
"RTN","VIABMS",8,0)
 ; RPC VIAB BMS
"RTN","VIABMS",9,0)
 ; ICR 10035    PATIENT FILE
"RTN","VIABMS",10,0)
 ; ICR 10040    HOSPITAL LOCATION FILE [File #44;fields .01,1,42]
"RTN","VIABMS",11,0)
 ; ICR 10060    NEW PERSON FILE [File 200;fields .01,30] (supported)
"RTN","VIABMS",12,0)
 ; ICR 6609     WARD [Access to File #42;fields .01,.017,.2] (private)
"RTN","VIABMS",13,0)
 ; ICR 4782     CLINIC PHONE [File #44;field 99]
"RTN","VIABMS",14,0)
 ; ICR 2652     DBIA2652 [File #42.4;field .01] (controlled)
"RTN","VIABMS",15,0)
 ; ICR 4433     NAME: DBIA4433 [API SDAPI^SDAMA301] (supported)
"RTN","VIABMS",16,0)
 ; ICR 5771     ORDERS FILE DATA [File #100;field #5] (controlled)
"RTN","VIABMS",17,0)
 ; ICR 6610     FACILITY MOVEMENT File #405.1;fields .01,.04] (private)
"RTN","VIABMS",18,0)
 ; ICR 6607     SC PERCENTAGE [File #2;field .302] (private)
"RTN","VIABMS",19,0)
 Q
"RTN","VIABMS",20,0)
 ;
"RTN","VIABMS",21,0)
EN(RESULT,VIA) ; entry point for RPC
"RTN","VIABMS",22,0)
 N VIATAG,VIAER
"RTN","VIABMS",23,0)
 S VIATAG=""
"RTN","VIABMS",24,0)
 I $O(VIA(""))="" S VIAER="Missing Parameters" D ERR(VIAER) Q
"RTN","VIABMS",25,0)
 I $G(VIA("PATH"))="" S VIAER="Missing PATH Parameters" D ERR(VIAER) Q
"RTN","VIABMS",26,0)
 ; -- parse array to parameters
"RTN","VIABMS",27,0)
 D PARSE(.VIA)
"RTN","VIABMS",28,0)
 D PATH(.VIATAG)
"RTN","VIABMS",29,0)
 I VIATAG'="" D @VIATAG
"RTN","VIABMS",30,0)
 D KVAR
"RTN","VIABMS",31,0)
 Q
"RTN","VIABMS",32,0)
 ;
"RTN","VIABMS",33,0)
PARSE(VIA) ; -- array parsing to parameters
"RTN","VIABMS",34,0)
 S VIAIENS=$G(VIA("IENS"))
"RTN","VIABMS",35,0)
 S VIAFLAGS=$G(VIA("FLAGS"))
"RTN","VIABMS",36,0)
 S VIAMAX=$G(VIA("MAX")) I VIAMAX>1000 S VIAMAX=1000
"RTN","VIABMS",37,0)
 I $G(VIAMAX)="" S VIAMAX=1000
"RTN","VIABMS",38,0)
 S VIAFROM=$G(VIA("FROM"))
"RTN","VIABMS",39,0)
 S VIATO=$G(VIA("TO")) ;search returns data up to a certain value for IEN.
"RTN","VIABMS",40,0)
 S VIAPART=$G(VIA("PART"))
"RTN","VIABMS",41,0)
 S VIAXREF=$G(VIA("XREF"))
"RTN","VIABMS",42,0)
 S VIASCRN=$G(VIA("SCREEN"))
"RTN","VIABMS",43,0)
 S VIAID=$G(VIA("ID"))
"RTN","VIABMS",44,0)
 S VIASDT=$G(VIA("SDATE"))
"RTN","VIABMS",45,0)
 S VIAEDT=$G(VIA("EDATE"))
"RTN","VIABMS",46,0)
 S VIALEDT=$G(VIA("LASTEDT"))
"RTN","VIABMS",47,0)
 S VIAMDT=$G(VIA("MOVDATE"))
"RTN","VIABMS",48,0)
 S VIAMTYP=$G(VIA("MOVTYPE"))
"RTN","VIABMS",49,0)
 S VIAPIEN=$G(VIA("PATIEN"))
"RTN","VIABMS",50,0)
 S VIACIEN=$G(VIA("CLNIEN"))
"RTN","VIABMS",51,0)
 S VIAOIEN=$G(VIA("ORDIEN"))
"RTN","VIABMS",52,0)
 S VIASSN=$G(VIA("SSN"))
"RTN","VIABMS",53,0)
 S VIAVAL=$G(VIA("VALUE"))
"RTN","VIABMS",54,0)
 Q 
"RTN","VIABMS",55,0)
 ;
"RTN","VIABMS",56,0)
PATH(VIATAG) ;The PATH parameter determines the line tag executed and data returned by the RPC.
"RTN","VIABMS",57,0)
 N X,I
"RTN","VIABMS",58,0)
 S X=""
"RTN","VIABMS",59,0)
 F I=1:1 S X=$P($T(HNDL+I),";;",2) Q:(X="END")!(X="")  I $$UP^XLFSTR(VIA("PATH"))=$P(X,";") S VIATAG=$P(X,";",2) Q
"RTN","VIABMS",60,0)
 Q
"RTN","VIABMS",61,0)
 ;
"RTN","VIABMS",62,0)
GETPAT ;Returns patient information based on DFN from File #2;ICR-10035, ICR-6607
"RTN","VIABMS",63,0)
 ;Input - VIA("PATH")="GETPATIENT" [required]
"RTN","VIABMS",64,0)
 ;        VIA("IENS")=Patient DFN, [required]
"RTN","VIABMS",65,0)
 ;Data returned
"RTN","VIABMS",66,0)
 ; .01 Name,.02 Sex,.03 DateOfBirth,.09 SocialSecurityNumber,.097 DateEnteredIntoFile,
"RTN","VIABMS",67,0)
 ;  .103 TreatingSpecialty,.1041 AttendingPhysician,.302 ServiceConnectedPercentage,.105 CurrentAdmission,.1 Diagnosis [Short] from File #405
"RTN","VIABMS",68,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS,X,VAL
"RTN","VIABMS",69,0)
 I VIAIENS="" S VIAER="Missing IENS Parameters" D ERR(VIAER) Q
"RTN","VIABMS",70,0)
 S VIAFILE=2,VIAFIELDS=".01;.02;.03;.09;.097;.103;.1041;.302;.105;",VIAFLAGS="IE"
"RTN","VIABMS",71,0)
 D GDIQ
"RTN","VIABMS",72,0)
 ; add Field .1 DIAGNOSIS [SHORT] from File #405 to result of .105 field in File #2.
"RTN","VIABMS",73,0)
 S X=1 F  S X=$O(RESULT(X)) Q:'X  I RESULT(X)["^.105^" S VAL=$P(RESULT(X),U,4) I VAL'="" S VAL=$$GET1^DIQ(405,VAL,.1,"E"),$P(RESULT(X),U,4)=$P(RESULT(X),U,4)_"~"_VAL Q
"RTN","VIABMS",74,0)
 Q
"RTN","VIABMS",75,0)
 ;
"RTN","VIABMS",76,0)
PATSSN ;Returns patient information based on SSN from File #2
"RTN","VIABMS",77,0)
 ;Input - VIA("PATH")="LISTPATIENTBYSSN" [required]
"RTN","VIABMS",78,0)
 ;        VIA("SSN")=SSN [required]
"RTN","VIABMS",79,0)
 N DFN
"RTN","VIABMS",80,0)
 S DFN=$O(^DPT("SSN",VIASSN,""))
"RTN","VIABMS",81,0)
 I DFN="" S VIAER="Invalid SSN" D ERR(VIAER) Q
"RTN","VIABMS",82,0)
 S VIAIENS=DFN_","
"RTN","VIABMS",83,0)
 D GETPAT
"RTN","VIABMS",84,0)
 Q
"RTN","VIABMS",85,0)
 ;
"RTN","VIABMS",86,0)
LADMPAT ;Returns a list of admitted patients from File #2
"RTN","VIABMS",87,0)
 ;Input - VIA("PATH")="LISTADMITTEDPATIENTSFORUPDATE" [required]
"RTN","VIABMS",88,0)
 ;        VIA("MAX")=n [optional]
"RTN","VIABMS",89,0)
 N VIAXREF,VIASCRN,X,VAL
"RTN","VIABMS",90,0)
 S VIAXREF="ACA",VIASCRN="I $D(^DPT(Y,.105))"
"RTN","VIABMS",91,0)
 D GPAT
"RTN","VIABMS",92,0)
 ; add Field .1 DIAGNOSIS [SHORT] from File #405 to result of .105 field in File #2.
"RTN","VIABMS",93,0)
 S X=1 F  S X=$O(RESULT(X)) Q:'X  S VAL=$P(RESULT(X),U,10) I VAL'="" S VAL=$$GET1^DIQ(405,VAL,.1,"E"),$P(RESULT(X),U,10)=$P(RESULT(X),U,10)_"~"_VAL
"RTN","VIABMS",94,0)
 Q
"RTN","VIABMS",95,0)
 ;
"RTN","VIABMS",96,0)
LSTPAT ;Returns a list of patients from File #2;ICR-10035, ICR-6607
"RTN","VIABMS",97,0)
 ;Input - VIA("PATH")="LISTPATIENT" [required]
"RTN","VIABMS",98,0)
 ;        VIA("IENS")=Patient DFN, (multiple IENs separated by comma) [required, optional if date range provided]
"RTN","VIABMS",99,0)
 ;        VIA("SDATE")=Start Date for search [optional if patient DFN provided]
"RTN","VIABMS",100,0)
 ;        VIA("EDATE")=End Date for search [optional if patient DFN provided]
"RTN","VIABMS",101,0)
 ;        VIA("MAX")=n [optional]
"RTN","VIABMS",102,0)
 ;
"RTN","VIABMS",103,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS,TVIAIENS,TRESULT,Y,I,VALUE,FLDS,CNT,VAL,X
"RTN","VIABMS",104,0)
 I VIAIENS'="" S TVIAIENS=VIAIENS S VIAIENS=""
"RTN","VIABMS",105,0)
 I $G(TVIAIENS)'="" D  Q
"RTN","VIABMS",106,0)
 . S VIAFILE=2,VIAFIELDS=".01;.02;.03;.09;.097;.103;.1041;.302;.105;",VIAFLAGS="I"
"RTN","VIABMS",107,0)
 . S FLDS=".01-2;.02-3;.03-4;.09-5;.097-6;.103-7;.1041-8;.105-10;.302-9;"
"RTN","VIABMS",108,0)
 . S TRESULT(1)="[Data]",CNT=1,VIA("MAX")=""
"RTN","VIABMS",109,0)
 . F I=1:1:$L(TVIAIENS,",") S VIAIENS=$P(TVIAIENS,",",I) I VIAIENS'="" D  M RESULT=TRESULT
"RTN","VIABMS",110,0)
 . . S $P(VALUE,U)=VIAIENS
"RTN","VIABMS",111,0)
 . . S VIAIENS=VIAIENS_","
"RTN","VIABMS",112,0)
 . . K RESULT
"RTN","VIABMS",113,0)
 . . D GDIQ
"RTN","VIABMS",114,0)
 . . I ($G(RESULT(1))'["Data")!($G(RESULT(2))="[ERROR]") K RESULT Q
"RTN","VIABMS",115,0)
 . . F Y=2:1:10 S J=$P($P(FLDS,";",Y-1),"-",2),$P(VALUE,U,J)=$P($G(RESULT(Y)),U,4) I $G(RESULT(Y))["^.105^" D
"RTN","VIABMS",116,0)
 . . . S VAL=$P($G(RESULT(Y)),U,4) I VAL'="" S VAL=$$GET1^DIQ(405,VAL,.1,"E"),$P(VALUE,U,J)=$P(VALUE,U,J)_"~"_VAL
"RTN","VIABMS",117,0)
 . . S CNT=CNT+1,TRESULT(CNT)=VALUE
"RTN","VIABMS",118,0)
 . . K RESULT
"RTN","VIABMS",119,0)
 D DTCHK(.RESULT,.VIASDT,.VIAEDT) I $D(RESULT) Q
"RTN","VIABMS",120,0)
 S VIASCRN="S X=$P($G(^DPT(Y,0)),U,9) I X?.N S X=$P($G(^DPT(Y,0)),U,16) I X>VIASDT,X<VIAEDT"
"RTN","VIABMS",121,0)
 D GPAT
"RTN","VIABMS",122,0)
 ; add Field .1 DIAGNOSIS [SHORT] from File #405 to result of .105 field in File #2.
"RTN","VIABMS",123,0)
 S X=1 F  S X=$O(RESULT(X)) Q:'X  S VAL=$P(RESULT(X),U,10) I VAL'="" S VAL=$$GET1^DIQ(405,VAL,.1,"E"),$P(RESULT(X),U,10)=$P(RESULT(X),U,10)_"~"_VAL
"RTN","VIABMS",124,0)
 Q
"RTN","VIABMS",125,0)
 ;
"RTN","VIABMS",126,0)
GPAT ;Get patient data from File #2;ICR-10035,ICR-6607
"RTN","VIABMS",127,0)
 ;Data returned
"RTN","VIABMS",128,0)
 ;  .01 Name,.02 Sex,.03 DateOfBirth,.09 SocialSecurityNumber,.097 DateEnteredIntoFile,.103 TreatingSpecialty
"RTN","VIABMS",129,0)
 ;  .1041 AttendingPhysician,.302 ServiceConnectedPercentage,.105 CurrentAdmission
"RTN","VIABMS",130,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS
"RTN","VIABMS",131,0)
 S VIAFILE=2,VIAFIELDS="@;.01;.02;.03;.09;.097;.103;.1041;.302;.105;",VIAFLAGS="IP"
"RTN","VIABMS",132,0)
 D LDIC
"RTN","VIABMS",133,0)
 Q
"RTN","VIABMS",134,0)
 ;
"RTN","VIABMS",135,0)
ADMTPAT ;Returns a list of admitted patients from File #2 up to the Admitted IEN;ICR-10035
"RTN","VIABMS",136,0)
 ;Input - VIA("PATH")="LISTADMITTEDPATIENTS" [required]
"RTN","VIABMS",137,0)
 ;        VIA("TO")=Admission IEN [required]
"RTN","VIABMS",138,0)
 ;        VIA("MAX")=n [optional]
"RTN","VIABMS",139,0)
 ;Data returned
"RTN","VIABMS",140,0)
 ;   .01 Name,.09 Social Security Number,.101 Bed Name,.102 Movement IEN,.105 Admitting Diagnosis,.1  Ward Name
"RTN","VIABMS",141,0)
 N VIAFILE,VIAFIELDS,VIASCRN,VIAFLAGS
"RTN","VIABMS",142,0)
 I VIATO="" S VIAER="Missing Admission IEN Parameter" D ERR(VIAER) Q
"RTN","VIABMS",143,0)
 S VIAFILE=2,VIAFIELDS="@;.01;.09;.101;.102;.105;.1;",VIATO=VIATO+.01,VIAXREF="ACA"
"RTN","VIABMS",144,0)
 S VIASCRN="I $D(^DPT(Y,.105)),$P(^DPT(Y,.105),U,1)<"_VIATO,VIAFLAGS="IP"
"RTN","VIABMS",145,0)
 D LDIC
"RTN","VIABMS",146,0)
 Q
"RTN","VIABMS",147,0)
 ;
"RTN","VIABMS",148,0)
WRDLOC ;Returns information for a ward from File #42;ICR-6609
"RTN","VIABMS",149,0)
 ;Input - VIA("PATH")="GETWARDLOCATIONS" [required]
"RTN","VIABMS",150,0)
 ;        VIA("IENS")=Ward IEN, [required]
"RTN","VIABMS",151,0)
 ;Data returned
"RTN","VIABMS",152,0)
 ;   .01 Name, .017 Specialty, .2 IsCurrentlyOutOfService
"RTN","VIABMS",153,0)
 N VIAFILE,VIAFIELDS
"RTN","VIABMS",154,0)
 I VIAIENS="" S VIAER="Missing Ward IEN" D ERR(VIAER) Q
"RTN","VIABMS",155,0)
 S VIAFILE=42,VIAFIELDS=".01;.017;.2;",VIAFLAGS="IE"
"RTN","VIABMS",156,0)
 D GDIQ
"RTN","VIABMS",157,0)
 Q
"RTN","VIABMS",158,0)
 ;
"RTN","VIABMS",159,0)
HOSLOC ;Returns a list of hospital locations from File #44;ICR-10040;ICR-4782
"RTN","VIABMS",160,0)
 ;Input - VIA("PATH")="LISTHOSPITALLOCATIONS" [required]
"RTN","VIABMS",161,0)
 ;        VIA("IENS")=Hospital IEN, (multiple IENs separated by comma) [optional]
"RTN","VIABMS",162,0)
 ;Data returned
"RTN","VIABMS",163,0)
 ;   .01 Name,1 Abbreviation,99 Telephone,42 WardLocation
"RTN","VIABMS",164,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS,TVIAIENS
"RTN","VIABMS",165,0)
 S VIAFILE=44,VIAFIELDS="@;.01;1;99;42;",VIAFLAGS="IP"
"RTN","VIABMS",166,0)
 I VIAIENS'="" S TVIAIENS=VIAIENS S VIAIENS="",VIAMAX=""
"RTN","VIABMS",167,0)
 D LDIC
"RTN","VIABMS",168,0)
 I $G(TVIAIENS)'="" S VIAIENS=TVIAIENS D PIENS^VIABMS1
"RTN","VIABMS",169,0)
 Q
"RTN","VIABMS",170,0)
 ;
"RTN","VIABMS",171,0)
NEWPER ;Returns a list of people from the NEW PERSON file #200 for a date range on DATE ENTERED Filed (#30)
"RTN","VIABMS",172,0)
 ;Input - VIA("PATH")="LISTNEWPERSON" [required]
"RTN","VIABMS",173,0)
 ;        VIA("IENS")=New Person IEN, (multiple IENs separated by comma) [optional]
"RTN","VIABMS",174,0)
 ;        VIA("SDATE")=Start Date for search [required, if VIA(IENS) not present]
"RTN","VIABMS",175,0)
 ;        VIA("EDATE")=End Date for search [required, if VIA(IENS) not present] 
"RTN","VIABMS",176,0)
 ;Data returned;ICR #10060
"RTN","VIABMS",177,0)
 ;   .01 Name, 30  Date Entered
"RTN","VIABMS",178,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS,TVIAIENS,TRESULT
"RTN","VIABMS",179,0)
 S VIAFILE=200,VIAFIELDS="@;.01;30;",VIAFLAGS="IP",VIAXREF="B"
"RTN","VIABMS",180,0)
 I VIAIENS'="" S TVIAIENS=VIAIENS S VIAIENS=""
"RTN","VIABMS",181,0)
 I $G(TVIAIENS)'="" S VIAFIELDS=".01;30;",VIAFLAGS="I" D  Q
"RTN","VIABMS",182,0)
 . S TRESULT(1)="[Data]",CNT=1
"RTN","VIABMS",183,0)
 . F I=1:1:$L(TVIAIENS,",") S VIAIENS=$P(TVIAIENS,",",I) I VIAIENS'="" D  M RESULT=TRESULT
"RTN","VIABMS",184,0)
 . . S VIAIENS=VIAIENS_","
"RTN","VIABMS",185,0)
 . . K RESULT
"RTN","VIABMS",186,0)
 . . D GDIQ
"RTN","VIABMS",187,0)
 . . I $G(RESULT(1))'["Data" Q
"RTN","VIABMS",188,0)
 . . S CNT=CNT+1,TRESULT(CNT)=$TR(VIAIENS,",")_"^"_$P($G(RESULT(2)),U,4)_"^"_$P($G(RESULT(3)),U,4)
"RTN","VIABMS",189,0)
 . . K RESULT
"RTN","VIABMS",190,0)
 D DTCHK(.RESULT,.VIASDT,.VIAEDT) I $D(RESULT) Q
"RTN","VIABMS",191,0)
 S VIASCRN="S X=$P($G(^(1)),U,7) I X>VIASDT,X<VIAEDT"
"RTN","VIABMS",192,0)
 D LDIC
"RTN","VIABMS",193,0)
 Q
"RTN","VIABMS",194,0)
 ;
"RTN","VIABMS",195,0)
SPLTY ;Returns a list of specialties from the SPECIALTY file #42.4;ICR-2652
"RTN","VIABMS",196,0)
 ;Input - VIA("PATH")="LISTSPECIALTY" [required]
"RTN","VIABMS",197,0)
 ;        VIA("IENS")= Specialty IEN, (multiple IENs separated by comma) [optional] 
"RTN","VIABMS",198,0)
 ;Data returned
"RTN","VIABMS",199,0)
 ;   .01 Name
"RTN","VIABMS",200,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS,TVIAIENS
"RTN","VIABMS",201,0)
 S VIAFILE=42.4,VIAFIELDS="@;.01;",VIAFLAGS="IP"
"RTN","VIABMS",202,0)
 I VIAIENS'="" S TVIAIENS=VIAIENS S VIAIENS="",VIAMAX=""
"RTN","VIABMS",203,0)
 D LDIC
"RTN","VIABMS",204,0)
 I $G(TVIAIENS)'="" S VIAIENS=TVIAIENS D PIENS^VIABMS1
"RTN","VIABMS",205,0)
 Q
"RTN","VIABMS",206,0)
 ;
"RTN","VIABMS",207,0)
PATAPPT ; Returns a list of patient appointments using API SDAPI^SDAMA301;ICR-4433
"RTN","VIABMS",208,0)
 ;Input - VIA("PATH")="LISTPATIENTAPPOINTMENT" [required]
"RTN","VIABMS",209,0)
 ;        VIA("IENS")=Patient IEN [required]
"RTN","VIABMS",210,0)
 ;        VIA("CLNIEN")=Hospital Location IEN [optional]
"RTN","VIABMS",211,0)
 ;        VIA("SDATE")=Start Date for search [optional]
"RTN","VIABMS",212,0)
 ;        VIA("EDATE")=End Date for search [optional]
"RTN","VIABMS",213,0)
 ;Data returned
"RTN","VIABMS",214,0)
 ;    .01 HospitalLocation, 20 DateAppointmentMade, 100 CurrentStatus
"RTN","VIABMS",215,0)
 N VIARRAY,CNT,VIADT,VIADFN,VIAPPT,VIACNT,CLNIEN,RCNT
"RTN","VIABMS",216,0)
 I VIAIENS="" S VIAER="Missing PATIENT IEN" D ERR(VIAER) Q 
"RTN","VIABMS",217,0)
 I (VIASDT'="")!(VIAEDT'="") D DTCHK(.RESULT,.VIASDT,.VIAEDT) I $D(RESULT) Q 
"RTN","VIABMS",218,0)
 S VIADFN=$TR(VIAIENS,","),VIAEDT=$S(VIAEDT="":DT,1:VIAEDT)
"RTN","VIABMS",219,0)
 S RESULT(1)="[Data]",CNT=1,RCNT=1
"RTN","VIABMS",220,0)
 S VIARRAY(1)=$P(VIASDT,".")_";"_$P(VIAEDT,".")
"RTN","VIABMS",221,0)
 I VIACIEN'="" S VIARRAY(2)=VIACIEN
"RTN","VIABMS",222,0)
 S VIARRAY(4)=VIADFN
"RTN","VIABMS",223,0)
 S VIARRAY("FLDS")="1;2;16;22"
"RTN","VIABMS",224,0)
 S VIACNT=$$SDAPI^SDAMA301(.VIARRAY)
"RTN","VIABMS",225,0)
 I VIACNT<1 Q
"RTN","VIABMS",226,0)
 I VIACNT>VIAMAX S RESULT(1)="[Misc]",RESULT(2)="[More]",RESULT(3)="[Data]",CNT=3
"RTN","VIABMS",227,0)
 S CLNIEN=$S(VIAFROM'="":$P(VIAFROM,"^")-1,1:0),VIADT=$S(VIAFROM'="":$P(VIAFROM,"^",2),1:0)
"RTN","VIABMS",228,0)
 F  S CLNIEN=$O(^TMP($J,"SDAMA301",VIADFN,CLNIEN)) Q:'CLNIEN  D  I RCNT>VIAMAX S RESULT(2)=RESULT(2)_"^"_CLNIEN_"^"_VIADT Q
"RTN","VIABMS",229,0)
 . S VIADT=$S((VIAFROM'="")&(RCNT=1):$P(VIAFROM,"^",2),1:0)
"RTN","VIABMS",230,0)
 . F  S VIADT=$O(^TMP($J,"SDAMA301",VIADFN,CLNIEN,VIADT)) Q:'VIADT  D  I RCNT>VIAMAX Q
"RTN","VIABMS",231,0)
 . . S VIAPPT=$G(^TMP($J,"SDAMA301",VIADFN,CLNIEN,VIADT)) ;appointment data
"RTN","VIABMS",232,0)
 . . I (VIADT<VIASDT)!(VIADT>VIAEDT) Q
"RTN","VIABMS",233,0)
 . . S CNT=CNT+1,RCNT=RCNT+1,RESULT(CNT)=VIADT_"^"_CLNIEN_"^"_$P(VIAPPT,"^",16)_"^"_$P($P(VIAPPT,"^",22),";",3)
"RTN","VIABMS",234,0)
 K ^TMP($J,"SDAMA301")
"RTN","VIABMS",235,0)
 Q
"RTN","VIABMS",236,0)
 ;
"RTN","VIABMS",237,0)
CANORDS ; Returns a list of cancelled orders from the ORDER file #100;ICR-5771
"RTN","VIABMS",238,0)
 ;Input - VIA("PATH")=VIA("PATH")="LISTCANCELORDERS" [required]
"RTN","VIABMS",239,0)
 ;        VIA("IENS")=Order IEN [required]
"RTN","VIABMS",240,0)
 ;Data returned
"RTN","VIABMS",241,0)
 ;    .5 OrderStatus
"RTN","VIABMS",242,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS
"RTN","VIABMS",243,0)
 S VIAFILE=100,VIAFIELDS="5",VIAFLAGS="IE"
"RTN","VIABMS",244,0)
 I VIAIENS="" S VIAER="Missing ORDER IEN" D ERR(VIAER) Q
"RTN","VIABMS",245,0)
 D GDIQ
"RTN","VIABMS",246,0)
 Q
"RTN","VIABMS",247,0)
 ;
"RTN","VIABMS",248,0)
LWRDLOC ; Returns a list of ward locations from the WARD LOCATION file #42;ICR-6609
"RTN","VIABMS",249,0)
 ;Input - VIA("PATH")="LISTWARDLOCATION" [required]
"RTN","VIABMS",250,0)
 ;        VIA("IENS")= Ward Location IEN, (multiple IENs separated by comma) [optional] 
"RTN","VIABMS",251,0)
 ;Data returned
"RTN","VIABMS",252,0)
 ;    .01 Name,.017 Specialty,.2 IsCurrentlyOutOfService
"RTN","VIABMS",253,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS,TVIAIENS
"RTN","VIABMS",254,0)
 S VIAFILE=42,VIAFIELDS="@;.01;.017;.2",VIAFLAGS="IP"
"RTN","VIABMS",255,0)
 I VIAIENS'="" S TVIAIENS=VIAIENS S VIAIENS="",VIAMAX=""
"RTN","VIABMS",256,0)
 D LDIC
"RTN","VIABMS",257,0)
 I $G(TVIAIENS)'="" S VIAIENS=TVIAIENS D PIENS^VIABMS1
"RTN","VIABMS",258,0)
 Q
"RTN","VIABMS",259,0)
 ;
"RTN","VIABMS",260,0)
MOVTYP ; Returns a list of facility movement type from the FACILITY MOVEMENT TYPE file #405.1;ICR-6610
"RTN","VIABMS",261,0)
 ;Input - VIA("PATH")="LISTFACILITYMOVEMENTTYPE" [required]
"RTN","VIABMS",262,0)
 ;        VIA("IENS")= Facility Movement Type IEN, (multiple IENs separated by comma) [optional]
"RTN","VIABMS",263,0)
 ;Data returned
"RTN","VIABMS",264,0)
 ;    .01 Name,.04 Active
"RTN","VIABMS",265,0)
 N VIAFILE,VIAFIELDS,VIAFLAGS,TVIAIENS
"RTN","VIABMS",266,0)
 S VIAFILE=405.1,VIAFIELDS="@;.01;.04",VIAFLAGS="IP"
"RTN","VIABMS",267,0)
 I VIAIENS'="" S TVIAIENS=VIAIENS S VIAIENS="",VIAMAX=""
"RTN","VIABMS",268,0)
 D LDIC
"RTN","VIABMS",269,0)
 I $G(TVIAIENS)'="" S VIAIENS=TVIAIENS D PIENS^VIABMS1
"RTN","VIABMS",270,0)
 Q
"RTN","VIABMS",271,0)
 ;
"RTN","VIABMS",272,0)
DTCHK(RESULT,VIASDT,VIAEDT) ;check/set date
"RTN","VIABMS",273,0)
 I (VIASDT="")!(VIAEDT="") S VIAER="Missing Date Parameters" D ERR(VIAER) Q
"RTN","VIABMS",274,0)
 S VIASDT=VIASDT-.000001,VIAEDT=$S(VIAEDT[".":VIAEDT+.000001,1:VIAEDT+.999999)
"RTN","VIABMS",275,0)
 Q
"RTN","VIABMS",276,0)
 ;
"RTN","VIABMS",277,0)
PATCHK(DFN) ;check if patient is valid in File #2
"RTN","VIABMS",278,0)
 D PID^VADPT
"RTN","VIABMS",279,0)
 I $G(VAERR) S VIAER="Invalid Patient IEN" D ERR(VIAER)
"RTN","VIABMS",280,0)
 K VA,VAERR
"RTN","VIABMS",281,0)
 Q
"RTN","VIABMS",282,0)
 ;
"RTN","VIABMS",283,0)
GDIQ ;Runs GETS^DIQ
"RTN","VIABMS",284,0)
 N VIADATA,VIAERR,Y,VIAFLD,N,X,J,C
"RTN","VIABMS",285,0)
 D GETS^DIQ(VIAFILE,VIAIENS,VIAFIELDS,VIAFLAGS,"VIADATA","VIAERR")
"RTN","VIABMS",286,0)
 S N=0
"RTN","VIABMS",287,0)
 D SET("[Data]")
"RTN","VIABMS",288,0)
 S VIAFLD=0 F  S VIAFLD=$O(VIADATA(VIAFILE,VIAIENS,VIAFLD)) Q:'VIAFLD  D
"RTN","VIABMS",289,0)
 . S X=VIAFILE_"^"_$E(VIAIENS,1,$L(VIAIENS)-1)_"^"_VIAFLD_"^"
"RTN","VIABMS",290,0)
 . ; -- below call to $$GET1 is too slow...working w/FM team for speed
"RTN","VIABMS",291,0)
 . ;IF $$GET1^DID(VIAFILE,VIAFLD,"","TYPE")="WORD-PROCESSING" D
"RTN","VIABMS",292,0)
 . IF $P($G(^DD(VIAFILE,VIAFLD,0)),U,4)[";0" D
"RTN","VIABMS",293,0)
 . . D SET(X_"[WORD PROCESSING]")
"RTN","VIABMS",294,0)
 . . S J=0 F  S J=$O(VIADATA(VIAFILE,VIAIENS,VIAFLD,J)) Q:'J  D
"RTN","VIABMS",295,0)
 . . . D SET(VIADATA(VIAFILE,VIAIENS,VIAFLD,J))
"RTN","VIABMS",296,0)
 . . D SET("$$END$$")
"RTN","VIABMS",297,0)
 . E  D
"RTN","VIABMS",298,0)
 . . D SET(X_$G(VIADATA(VIAFILE,VIAIENS,VIAFLD,"I"))_"^"_$G(VIADATA(VIAFILE,VIAIENS,VIAFLD,"E")))
"RTN","VIABMS",299,0)
 ;
"RTN","VIABMS",300,0)
 IF $D(VIAERR) D
"RTN","VIABMS",301,0)
 . D SET("[ERROR]")
"RTN","VIABMS",302,0)
 ;
"RTN","VIABMS",303,0)
 M RESULT=Y
"RTN","VIABMS",304,0)
 Q
"RTN","VIABMS",305,0)
 ; 
"RTN","VIABMS",306,0)
LDIC ;Runs LIST^DIC
"RTN","VIABMS",307,0)
 N VIAERR,Y,I,N
"RTN","VIABMS",308,0)
 D LIST^DIC(VIAFILE,VIAIENS,VIAFIELDS,VIAFLAGS,VIAMAX,.VIAFROM,VIAPART,VIAXREF,VIASCRN,VIAID,"^TMP(""VIARSLT"",$J)","VIAERR")
"RTN","VIABMS",309,0)
 S N=0
"RTN","VIABMS",310,0)
 IF $G(VIAFROM)]"" D
"RTN","VIABMS",311,0)
 . D SET("[Misc]")
"RTN","VIABMS",312,0)
 . D SET("MORE"_U_VIAFROM_U_VIAFROM("IEN"))
"RTN","VIABMS",313,0)
 ;
"RTN","VIABMS",314,0)
 D SET("[Data]")
"RTN","VIABMS",315,0)
 S I=0 F  S I=$O(^TMP("VIARSLT",$J,"DILIST",I)) Q:'I  D SET($G(^TMP("VIARSLT",$J,"DILIST",I,0)))
"RTN","VIABMS",316,0)
 ;
"RTN","VIABMS",317,0)
 IF $D(VIAERR) D
"RTN","VIABMS",318,0)
 . D SET("[Errors]")
"RTN","VIABMS",319,0)
 . D SET($G(VIAERR("DIERR",1,"TEXT",1)))
"RTN","VIABMS",320,0)
 ;
"RTN","VIABMS",321,0)
 M RESULT=Y
"RTN","VIABMS",322,0)
 K ^TMP("VIARSLT",$J)
"RTN","VIABMS",323,0)
 Q
"RTN","VIABMS",324,0)
 ;
"RTN","VIABMS",325,0)
SET(X) ;
"RTN","VIABMS",326,0)
 S N=N+1
"RTN","VIABMS",327,0)
 S Y(N)=X
"RTN","VIABMS",328,0)
 Q
"RTN","VIABMS",329,0)
 ;
"RTN","VIABMS",330,0)
ERR(X) ;Error processing
"RTN","VIABMS",331,0)
 N N
"RTN","VIABMS",332,0)
 S N=0
"RTN","VIABMS",333,0)
 D SET("[Errors]")
"RTN","VIABMS",334,0)
 D SET(X)
"RTN","VIABMS",335,0)
 M RESULT=Y
"RTN","VIABMS",336,0)
 Q
"RTN","VIABMS",337,0)
 ;
"RTN","VIABMS",338,0)
KVAR ;Clean-up
"RTN","VIABMS",339,0)
 K VIAFILE,VIAFIELDS,VIAIENS,VIAEDT,VIAFLAGS,VIAID,VIAMAX,VIAPART,VIASCRN,VIASDT,VIAVAL,VIAXREF,VIAFROM
"RTN","VIABMS",340,0)
 K VIAPIEN,VIACIEN,VIAMDT,VIAMTYP,VIATO,VIALEDT,VIASSN,VIAA,VIAB,VIAC,VIAD,VIAR,VIAV,VIAX,VIAOIEN,VIA3,X,Y
"RTN","VIABMS",341,0)
 Q
"RTN","VIABMS",342,0)
 ;
"RTN","VIABMS",343,0)
HNDL ;Finds PATH and linetag that needs to be executed for results
"RTN","VIABMS",344,0)
 ;;GETPATIENT;GETPAT
"RTN","VIABMS",345,0)
 ;;LISTPATIENTBYSSN;PATSSN
"RTN","VIABMS",346,0)
 ;;LISTADMITTEDPATIENTSFORUPDATE;LADMPAT
"RTN","VIABMS",347,0)
 ;;LISTPATIENT;LSTPAT
"RTN","VIABMS",348,0)
 ;;LISTADMITTEDPATIENTS;ADMTPAT
"RTN","VIABMS",349,0)
 ;;GETWARDLOCATIONS;WRDLOC
"RTN","VIABMS",350,0)
 ;;LISTHOSPITALLOCATIONS;HOSLOC
"RTN","VIABMS",351,0)
 ;;LISTNEWPERSON;NEWPER
"RTN","VIABMS",352,0)
 ;;LISTSPECIALTY;SPLTY
"RTN","VIABMS",353,0)
 ;;LISTPATIENTAPPOINTMENT;PATAPPT
"RTN","VIABMS",354,0)
 ;;LISTCANCELORDERS;CANORDS
"RTN","VIABMS",355,0)
 ;;LISTWARDLOCATION;LWRDLOC
"RTN","VIABMS",356,0)
 ;;LISTFACILITYMOVEMENTTYPE;MOVTYP
"RTN","VIABMS",357,0)
 ;;LISTFACILITYTREATINGSPECIALTY;TRTSPTY^VIABMS1
"RTN","VIABMS",358,0)
 ;;LISTMASMOVEMENTTRANSACTIONTYPE;MASTYP^VIABMS1
"RTN","VIABMS",359,0)
 ;;LISTMEDICALCENTERDIVISION;MEDCTR^VIABMS1
"RTN","VIABMS",360,0)
 ;;LISTORDERABLEITEM;ORDITM^VIABMS1
"RTN","VIABMS",361,0)
 ;;LISTORDERSTATUS;ORDSTA^VIABMS1
"RTN","VIABMS",362,0)
 ;;LISTBEDSWITCH;BEDSWCH^VIABMS1
"RTN","VIABMS",363,0)
 ;;GETPATIENTMOVEMENT;GPATMVT^VIABMS1
"RTN","VIABMS",364,0)
 ;;LISTPATIENTMOVEMENT;LPATMVT^VIABMS1
"RTN","VIABMS",365,0)
 ;;LISTPATIENTMOVEMENTSBYADMISSION;APATMVT^VIABMS1
"RTN","VIABMS",366,0)
 ;;LISTSCHEDULEDADMISSION;SCHADM^VIABMS1
"RTN","VIABMS",367,0)
 ;;LISTROOMBED;RMBED^VIABMS1
"RTN","VIABMS",368,0)
 ;;LISTCLINICAPPOINTMENTS;CLNAPPT^VIABMS1
"RTN","VIABMS",369,0)
 ;;LISTORDERS;LSTORD^VIABMS1
"RTN","VIABMS",370,0)
 ;;LISTORDERACTIONS;ORDACT^VIABMS1
"RTN","VIABMS",371,0)
 ;;END
"VER")
8.0^22.2
"BLD",10018,6)
^9
**END**
**END**

